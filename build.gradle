import org.apache.tools.ant.taskdefs.condition.Os
import org.apache.tools.ant.filters.*

plugins {
  id "com.github.spotbugs" version "6.0.4" apply false
  id "org.sonarqube" version "4.4.1.3373"
  id "org.owasp.dependencycheck" version "9.0.7" apply false
}

ext {
  interlokCoreVersion = project.findProperty('interlokCoreVersion') ?: '5.0-SNAPSHOT'
  releaseVersion = project.findProperty('releaseVersion') ?: '5.0-SNAPSHOT'
  nexusBaseUrl = project.findProperty('nexusBaseUrl') ?: 'https://nexus.adaptris.net/nexus'
  mavenPublishUrl = project.findProperty('mavenPublishUrl') ?: nexusBaseUrl + '/content/repositories/snapshots'
  javadocsBaseUrl = nexusBaseUrl + "/content/sites/javadocs/com/adaptris"
  repoUsername = project.findProperty('repoUsername') ?: 'unknown'
  repoPassword = project.findProperty('repoPassword') ?: 'unknown'
  defaultNexusRepo = project.findProperty('defaultNexusRepo') ?: 'unknown'
  interlokJavadocs= project.findProperty('interlokJavadocs') ?: javadocsBaseUrl + "/interlok-core/" + interlokCoreVersion
  interlokCommonJavadocs= project.findProperty('interlokCommonJavadocs') ?: javadocsBaseUrl + "/interlok-common/" + interlokCoreVersion
  offlineJavadocPackageDir = new File(project.buildDir, "offline-javadoc-packages")

  organizationName = "Adaptris Ltd"
  springVersion = '5.3.27'
  slf4jVersion = '2.0.9'
  mockitoVersion = '5.2.0'
}

ext.hostname = { ->
  if (Os.isFamily(Os.FAMILY_WINDOWS)) {
    return System.getenv("COMPUTERNAME")
  }
  return System.getenv("HOSTNAME")
}

// If graphviz is installed via scoop, plantuml doesn't find it because it's not
// in its expected "location(s)" it searches for c:\*\graphviz**\dot.exe
// so windows we'd expect to have the GRAPHVIZ_DOT env defined 
// On Linux we should be able to find it via the path.
ext.hasGraphViz = { ->
  def app = "dot"
  if (Os.isFamily(Os.FAMILY_WINDOWS)) {
    app = app + ".exe"
  }
  return System.getenv("GRAPHVIZ_DOT") !=null ||  System.getenv("PATH").split(File.pathSeparator).any{
    java.nio.file.Paths.get("${it}").resolve(app).toFile().exists()
  }
}

ext.gitBranchNameOrTimestamp = { branchName ->
  if (branchName.equals("HEAD") || branchName.equals("develop") || branchName.startsWith("release")) {
    return new Date().format('HH:mm:ss z');
  }
  return branchName;
}

ext.buildInfo = { ->
   new ByteArrayOutputStream().withStream { os ->
      exec {
        executable = "git"
        args = ["rev-parse", "--abbrev-ref", "HEAD"]
        standardOutput = os
      }
      def branchName = os.toString().replaceAll("\r", "").replaceAll("\n", "").trim();
      return gitBranchNameOrTimestamp(branchName);
    }
}

def propertyTemplate(dir, filename) {
  def file = new File(dir, filename + "." + hostname())
  if (file.exists()) {
    return file.getName()
  }
  return filename
}

task clean(type: Delete) {
  delete project.buildDir
}

sonar {
  properties {
    property "sonar.projectKey", "adaptris_interlok-jmx-jms"
    property "sonar.organization", "adaptris-org"
    property "sonar.host.url", "https://sonarcloud.io"
  }
}

allprojects {
  tasks.withType(Jar) {
    metaInf {
      from ("$rootDir") {
        include 'LICENSE*'
        include 'NOTICE*'
      }
    }
  }
  tasks.withType(GenerateModuleMetadata) {
    enabled = false
  }
}

subprojects { subproject ->
  apply plugin: "java-library"
  apply plugin: "maven-publish"
  apply plugin: "jacoco"
  apply plugin: "com.github.spotbugs"
  apply plugin: "org.sonarqube"
  apply plugin: "org.owasp.dependencycheck"

  sourceCompatibility = JavaVersion.VERSION_17
  targetCompatibility = JavaVersion.VERSION_17
  group   = 'com.adaptris'
  version = releaseVersion
  def versionDir = "$buildDir/version"

  repositories {
    mavenCentral()
    if (defaultNexusRepo != "unknown") {
      maven {
        url "$defaultNexusRepo"
        allowInsecureProtocol true
      }
    }
    maven { url "$nexusBaseUrl/content/groups/public" }
    maven { url "$nexusBaseUrl/content/groups/interlok" }
  }

  configurations {
    javadoc {}
    umlDoclet {}
    all*.exclude group: 'c3p0'
    all*.exclude group: 'commons-logging'
    all*.exclude group: 'javamail'
    all*.exclude group: 'javax.mail', module: 'mail'
    all*.exclude group: 'org.glassfish.hk2.external'
    all*.exclude group: 'xalan', module: 'xalan'
    all*.exclude group: 'net.sf.saxon', module: 'saxon'
    all*.exclude group: 'org.codehaus.woodstox'
    all*.exclude group: 'org.eclipse.jetty.orbit', module: 'javax.mail.glassfish'
    // INTERLOK-3197 exclude old javax.mail
    all*.exclude group: 'com.sun.mail', module: 'javax.mail'
    all*.exclude group: 'javax.validation', module: 'validation-api'
    all*.exclude group: 'javax.activation', module: 'activation'
    all*.exclude group: 'javax.activation', module: 'javax.activation-api'

    // INTERLOK-3740 switch from jcraft to com.github.mwiede jsch fork.
    all*.exclude group: 'com.jcraft', module: 'jsch'
  }

  configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
  }

  // In this section you declare the dependencies for your production and test code
  dependencies {
    api ("org.glassfish.external:opendmk_jmxremote_optional_jar:1.0-b01-ea")
    api ("commons-lang:commons-lang:2.6")
    api ("org.apache.commons:commons-lang3:3.14.0")
    api ("org.apache.geronimo.specs:geronimo-jms_1.1_spec:1.1.1")
    api ("org.springframework:spring-jms:$springVersion")
    api ("org.springframework:spring-beans:$springVersion") { transitive=false}
    api ("org.springframework:spring-core:$springVersion") { transitive=false}
    api ("org.springframework:spring-context:$springVersion") { transitive=false}
    api ("org.springframework:spring-aop:$springVersion") { transitive=false}
    api ("com.thoughtworks.xstream:xstream:1.4.20")
    api ("commons-io:commons-io:2.15.1")
    implementation ("aopalliance:aopalliance:1.0")
    implementation ("org.slf4j:slf4j-api:$slf4jVersion")

    umlDoclet("nl.talsmasoftware:umldoclet:2.1.0")
    annotationProcessor ("com.adaptris:interlok-core-apt:$interlokCoreVersion") {changing= true}

    testImplementation ("org.junit.jupiter:junit-jupiter-engine:5.10.1")
    testImplementation ("org.slf4j:jcl-over-slf4j:$slf4jVersion")
    testImplementation ("org.slf4j:slf4j-simple:$slf4jVersion")

    testImplementation ("org.mockito:mockito-core:$mockitoVersion")
    testImplementation ("org.mockito:mockito-inline:$mockitoVersion")

    javadoc("com.adaptris:interlok-core-apt:$interlokCoreVersion") { changing= true}
  }

  javadoc {
    onlyIf {
      !hasGraphViz()
    }
    configure(options) {
      taglets = ["com.adaptris.taglet.ConfigTaglet", "com.adaptris.taglet.LicenseTaglet"]
      options.addStringOption "tagletpath", configurations.javadoc.asPath
      options.addStringOption "Xdoclint:none", "-quiet"
    }
  }

  task umlJavadoc(type: Javadoc) {
    group 'Documentation'
    description 'Build javadocs using plantuml + graphviz + umldoclet, if dot is available'

    onlyIf {
      hasGraphViz()
    }
    source = sourceSets.main.allJava
    classpath = project.sourceSets.main.compileClasspath
    configure(options) {
      taglets = ["com.adaptris.taglet.ConfigTaglet", "com.adaptris.taglet.LicenseTaglet"]
      options.addStringOption "tagletpath", configurations.javadoc.asPath
      options.addStringOption "Xdoclint:none", "-quiet"
      options.docletpath = configurations.umlDoclet.files.asType(List)
      options.doclet = "nl.talsmasoftware.umldoclet.UMLDoclet"
      // Create class & package use pages
      options.addStringOption "use"    
      options.addStringOption "umlImageFormat", "SVG"
      options.addStringOption "umlExcludedTypeReferences", "java.lang.Exception,java.lang.Object,java.lang.Enum,java.lang.annotation.Annotation"
      options.addStringOption "umlJavaBeanPropertiesAsFields"
      options.addBooleanOption "failOnCyclicPackageDependencies", false
    }
  }

  sourceSets {
    main {
      output.dir(versionDir, builtBy: 'generateVersion')
    }
  }

  // Generate the META-INF/adaptris-version file
  task generateVersion {
    doLast {
      def versionFile = new File(new File(versionDir, 'META-INF'), 'adaptris-version')
      versionFile.getParentFile().mkdirs()
      ant.propertyfile(file: versionFile) {
        entry(key: 'component.name', value: componentName)
        entry(key: 'component.description', value: componentDesc)
        entry(key: 'build.version', value: releaseVersion)
        entry(key: 'groupId', value: project.group)
        entry(key: 'artifactId', value: project.name)
        entry(key: 'build.date', value: new Date().format('yyyy-MM-dd'))
        entry(key: 'build.info', value: buildInfo())
      }
    }
  }

  ext.testResourcesDir = { ->
    return "${project.projectDir}/src/test/resources".replaceAll("\\\\", "/")
  }

  task generateUnitTestProperties {
    doLast {
      ant.property(file: project.projectDir.getCanonicalPath() + "/" + "build.properties")
      ant.property(file:  testResourcesDir() + "/" + propertyTemplate(testResourcesDir(), "unit-tests.properties.template"))
      ant.echoproperties(destfile:  testResourcesDir() + '/unit-tests.properties.resolved', prefix: "junit.")
      ant.replace(file:  testResourcesDir() + '/unit-tests.properties.resolved', token:"junit.", value: "")
    }
  }

  task copyUnitTestProperties() {
    dependsOn(generateUnitTestProperties)
    doLast {
      delete file( testResourcesDir() + '/unit-tests.properties')
      ant.copy(file: testResourcesDir() + "/unit-tests.properties.resolved",
              tofile: testResourcesDir() + '/unit-tests.properties') {
        filterset {
          filter(token: "JAVA_HOME", value: System.getProperty("java.home").replaceAll("\\\\", "/"))
          filter(token: "BASE_DIR", value: project.projectDir.getCanonicalPath().replaceAll("\\\\", "/"))
          filter(token: "BUILD_DIR", value: project.buildDir.getCanonicalPath().replaceAll("\\\\", "/"))
        }
      }
    }
  }

  task deleteGeneratedFiles(type: Delete) {
    delete file(testResourcesDir() + "/unit-tests.properties"), file(testResourcesDir() + "/unit-tests.properties.resolved")
  }

  spotbugsMain {
    effort = com.github.spotbugs.snom.Effort.valueOf("MAX")
    reportLevel = com.github.spotbugs.snom.Confidence.valueOf("HIGH")
    reports {
      xml {
        required.set(false)
      }
      html {
        required.set(true)
        stylesheet = 'default.xsl'
      }
    }
	excludeFilter = new File("$rootDir/gradle/spotbugs-exclude.xml")
  }

  dependencyCheck  {
    suppressionFiles= [ "https://raw.githubusercontent.com/adaptris/interlok/develop/gradle/owasp-exclude.xml" , "$rootDir/gradle/owasp-exclude.xml"]
    scanConfigurations = [ "api", "compileClasspath", "runtimeClasspath" ]
    formats = [ "HTML", "JUNIT" ]
    junitFailOnCVSS = 7.0
    failBuildOnCVSS = 7.0
    analyzers {
      assemblyEnabled=false
      knownExploitedURL=project.findProperty('dependencyCheckKnownExploitedURL')
    }
  }

  jacocoTestReport {
    reports {
      xml.required= true
      html.required= true
    }
  }
  
  test {
    jvmArgs = ['--add-opens', 'java.base/java.lang=ALL-UNNAMED', '--add-opens', 'java.base/java.util=ALL-UNNAMED', '--add-opens', 'java.base/java.lang.reflect=ALL-UNNAMED', '--add-opens', 'java.management/javax.management=ALL-UNNAMED']
    useJUnitPlatform()
  }

  // disable spotbugsTests which checks our test code..
  spotbugsTest.enabled = false
  
  javadoc.dependsOn umlJavadoc
  check.dependsOn jacocoTestReport
  clean.dependsOn deleteGeneratedFiles
}
