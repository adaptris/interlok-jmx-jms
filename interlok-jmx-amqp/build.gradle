ext {
  componentName="Interlok JMX/JMS AMQP provider via QPID"
  nettyVersion="4.1.45.Final"
}

dependencies {
  compile project(':interlok-jmx-jms-common')
  compile ("org.apache.qpid:qpid-jms-client:0.49.0"){
    // 4.1.43.Final has a HTTP request smuggler vuln, so
    // we exclude netty, and force the fixed version
    // dependabot will sort us out...
    exclude group: "io.netty"
  }
  implementation ("io.netty:netty-buffer:$nettyVersion")
  implementation ("io.netty:netty-common:$nettyVersion")
  implementation ("io.netty:netty-handler:$nettyVersion")
  implementation ("io.netty:netty-transport:$nettyVersion")
  implementation ("io.netty:netty-transport-native-epoll:$nettyVersion")
  implementation ("io.netty:netty-transport-native-unix-common:$nettyVersion")
  implementation ("io.netty:netty-transport-native-kqueue:$nettyVersion")
  implementation ("io.netty:netty-codec:$nettyVersion")
  implementation ("io.netty:netty-codec-http:$nettyVersion")

  compile ("org.apache.qpid:proton-jms:0.12.2")
  compile ("org.apache.qpid:proton-j:0.33.3")

  testImplementation project(':interlok-jmx-jms-stubs')
  testImplementation ('junit:junit:4.13')

  testImplementation ("org.slf4j:jcl-over-slf4j:$slf4jVersion")
  testImplementation ("org.slf4j:slf4j-log4j12:$slf4jVersion")
  testImplementation ("org.apache.logging.log4j:log4j-core:$log4j2Version")
  testImplementation ("org.apache.logging.log4j:log4j-1.2-api:$log4j2Version")
  testImplementation ("org.apache.logging.log4j:log4j-api:$log4j2Version")

  testImplementation ("javax.management.j2ee:javax.management.j2ee-api:1.1.2")

}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

jar {
  manifest {
    attributes("Built-By": System.getProperty('user.name'),
              "Build-Jdk": System.getProperty('java.version'),
              "Implementation-Title": componentName,
              "Implementation-Version": project.version,
              "Implementation-Vendor-Id": project.group,
              "Implementation-Vendor": organizationName)
  }
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java

      artifact javadocJar { classifier "javadoc" }
      artifact sourcesJar { classifier "sources" }

      pom.withXml {
        asNode().appendNode("name", componentName)
        asNode().appendNode("description", "JMX/JMS AMQP Provider via Apache QPID")
        def properties = asNode().appendNode("properties")
        properties.appendNode("url", "http://interlok.adaptris.net/interlok-docs/advanced-jmx-jms.html")
        properties.appendNode("target", "3.10.0+")
        properties.appendNode("tags", "jmx,jms")
        properties.appendNode("license", "false")
        properties.appendNode("readme", "https://github.com/adaptris/interlok-jmx-jms/raw/develop/README.md")
      }
    }
  }
  repositories {
    maven {
      credentials {
        username repoUsername
        password repoPassword
      }
      url mavenPublishUrl
    }
  }
}

processTestResources.dependsOn copyUnitTestProperties
